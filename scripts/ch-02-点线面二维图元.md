## ***直线*** 
### 一条直线
从本章开始使用WebGL2，相应的GLSL着色器语言版本使用GLSL 3.0，所以要在着色器最开始就声明所使用的版本。注意在版本声明之前不能有任何其他的代码或者空行。

片元着色器：
```
<script id="fragmentShader" type="x-shader/x-fragment">#version 300 es
  precision highp float;

  in vec2 v_TexCoord;
  out vec4 fragColor; 

  void main()	{
    float width = 0.01;
    float d_1 = step(0.5, v_TexCoord.x);
    float d_2 = step(0.5 + width, v_TexCoord.x);
    float d = d_1 - d_2;

    fragColor = vec4(d);
  }
```
![canvas-preview](https://raw.githubusercontent.com/guoweish/the-codex-of-shader-graffiti/master/scripts/images/ch-02/1.jpg)

### 一群直线
片元着色器：
```
  ...
  float n = 30.0;
  float width = 0.01;

  float x = fract(v_TexCoord.x * n);
  float d_1 = step(0.5, x);
  float d_2 = step(0.5 + width, x);
  float d = d_1 - d_2;

  fragColor = vec4(d);
  ...
```
![canvas-preview](https://raw.githubusercontent.com/guoweish/the-codex-of-shader-graffiti/master/scripts/images/ch-02/2.jpg)

### 直线网格
片元着色器：
```
  ...
  float n = 10.0;
  float width = 0.03;

  vec2 xy = fract(st * n);
  vec2 d_1 = step(0.6, xy);
  vec2 d_2 = step(0.6 + width, xy);
  vec2 d = d_1 - d_2;
  float c = max(d.x, d.y);

  fragColor = vec4(c);
  ...
```
![canvas-preview](https://raw.githubusercontent.com/guoweish/the-codex-of-shader-graffiti/master/scripts/images/ch-02/3.jpg)

### 斜线
关于SDF的概念可以参考这里：
 [https://en.wikipedia.org/wiki/Signed_distance_function](https://en.wikipedia.org/wiki/Signed_distance_function)
片元着色器：
```
  ...
  float line(vec2 p, vec2 a, vec2 b, float width) {
    vec2 pa = p - a;
    vec2 ba = b - a;
    vec2 dir = normalize(ba);
    float t = clamp(dot(pa, dir), 0., length(ba));
    float dist = length(pa - dir * t);
    return 1.0 - step(dist, width);
  }

  ...
  float c = line(st, vec2(-0.3, -0.3), vec2(0.3, 0.3), 0.002);
  float color = step(c, 0.0);

  fragColor = vec4(vec3(color), 1.0);
  ...
```
![canvas-preview](https://raw.githubusercontent.com/guoweish/the-codex-of-shader-graffiti/master/scripts/images/ch-02/4.jpg)

## ***圆形***
片元着色器：
```
  ...
  float circle (vec2 uv, float radius) {
    return (length(uv) - radius);
  }
  
  ...
  float c = circle(st, 0.2);
  float color = step(c, 0.0);

  fragColor = vec4(vec3(color), 1.0);
  ...
```
![canvas-preview](https://raw.githubusercontent.com/guoweish/the-codex-of-shader-graffiti/master/scripts/images/ch-02/5.jpg)

片元着色器：
```
  ...
  float circle (vec2 uv, float radius) {
    return (length(uv) - radius);
  }

  vec2 translate(vec2 uv, vec2 offset) {
    return uv - offset;
  }
  
  ...
  st = translate(st, vec2(0.3, 0.2));

  float c = circle(st, 0.1);
  float color = step(c, 0.0);

  fragColor = vec4(vec3(color), 1.0);
  ...
```
![canvas-preview](https://raw.githubusercontent.com/guoweish/the-codex-of-shader-graffiti/master/scripts/images/ch-02/6.jpg)

## ***先后叠加绘制多个图形：Mix方法***
片元着色器：
```
  ...
  float createCircle (vec2 uv, float radius) {
    float d = length(uv) - radius;
    return (1.0 - step(d, 0.0));
  }

  float createBox(vec2 uv, vec2 b ) {
    vec2 d = abs(uv)-b;
    float e = length(max(d,vec2(0.0))) + min(max(d.x,d.y),0.0);
    return (1.0 - step(e, 0.0));
  }
  
  ...
  vec4 backgroundColor = vec4(1.0);
  vec4 circleColor_1 = vec4(1.0, 0.7, 0.3, 0.7);
  vec4 circleColor_2 = vec4(0.3, 1.0, 0.7, 0.7);
  vec4 circleColor_3 = vec4(0.7, 0.3, 1.0, 0.7);
  vec4 boxColor = vec4(1.0, 1.0, 1.0, 1.0);

  vec2 uv_1 = translate(uv, vec2(-0.2, 0.0));
  float circle_1 = createCircle(uv_1, 0.3);
  fragColor = mix(circleColor_1, backgroundColor, circle_1);

  vec2 uv_2 = translate(uv, vec2(0.0, 0.0));
  float circle_2 = createCircle(uv_2, 0.3);
  fragColor = mix(circleColor_2, fragColor, circle_2);

  vec2 uv_3 = translate(uv, vec2(0.2, 0.0));
  float circle_3 = createCircle(uv_3, 0.3);
  fragColor = mix(circleColor_3, fragColor, circle_3);

  fragColor = vec4(fragColor);
  ...
```
![canvas-preview](https://raw.githubusercontent.com/guoweish/the-codex-of-shader-graffiti/master/scripts/images/ch-02/7.jpg)

## ***案例：绘制时钟***
片元着色器：
```
  ...
  float circle(vec2 pixel, vec2 center, float radius, float blurSize) {
    vec2 vectorPixelToCenter = pixel - center;
    float distanceToCenter = length(vectorPixelToCenter);
    
    return smoothstep(radius - blurSize, radius, distanceToCenter);
  }

  float line(vec2 p, vec2 a, vec2 b, float width, float blur) {
    vec2 pa = p - a;
    vec2 ba = b - a;
    vec2 dir = normalize(ba);
    float t = clamp(dot(pa, dir), 0., length(ba));
    float dist = length(pa - dir * t);
    return smoothstep(width - blur, width, dist);
  }

  mat2 rotateMatrix(float angle) {
    return mat2(cos(angle), sin(angle), -sin(angle), cos(angle));
  }
  
  ...
  //shake the clock
  float tt=mod(u_Time,1.5)/1.5;
  float ss=1.0+0.5*sin(tt*3.1415*6.0+uv.x*0.5)*exp(-tt*4.0);
  uv*=(vec2(0.7,1.5)+ss*vec2(0.3,-0.5));

  //paint the clock
  float circleBodySign = circle(uv, vec2(0.0, 0.0), 0.3, 0.005);
  vec4 clockBodyColor = mix(vec4(0.7, 0.7, 0.7, 1.0), vec4(0, 0.0, 0.0, 0.0), circleBodySign);
  
  float circleBodyWhiteSign = circle(uv, vec2(0.0, 0.0), 0.28, 0.005);
  vec4 clockBodyWhiteColor = mix(vec4(1.0, 1.0, 1.0, 1.0), vec4(0.0, 0.0, 0.0, 0.0), circleBodyWhiteSign);
  
  fragColor = vec4(clockBodyColor);
  fragColor += clockBodyWhiteColor;
  
  mat2 rotationSecond = rotateMatrix(u_Time + 45.0/3.14);   
  float secondLineSign = line(uv, vec2(0.0, 0.2) * rotationSecond, vec2(0.0, -0.03) * rotationSecond, 0.005, 0.005);
  fragColor = mix(vec4(1.0, 0.0, 0.0, 1.0), fragColor, secondLineSign);
  
  mat2 rotationMinute = rotateMatrix(u_Time/5.0 + 15.0/6.283);
  float minuteLineSign = line(uv, vec2(0.0, 0.15) * rotationMinute, vec2(0.0, -0.03) * rotationMinute, 0.007, 0.005);
  fragColor = mix(vec4(0.0, 0.0, 0.0, 1.0), fragColor, minuteLineSign);
  
  mat2 rotationHour = rotateMatrix(u_Time/10.0 + 45.0/6.283);
  float hourLineSign = line(uv, vec2(0.0, 0.1) * rotationHour, vec2(0.0, -0.03) * rotationHour, 0.009, 0.005);
  fragColor = mix(vec4(0.0, 0.0, 0.0, 1.0), fragColor, hourLineSign);
  ...
```
![canvas-preview](https://raw.githubusercontent.com/guoweish/the-codex-of-shader-graffiti/master/scripts/images/ch-02/case-clock.gif)

注：该时钟案例参考了[iq原作](https://www.shadertoy.com/view/lsXGz8)、[陈剑鑫改进版](https://zhuanlan.zhihu.com/p/39798563?utm_source=ZHShareTargetIDMore&utm_medium=social&utm_oi=70060592857088)。